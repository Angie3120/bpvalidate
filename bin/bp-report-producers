#!/usr/bin/perl -w

use utf8;
use strict;
use EOSN::Report;
use List::Util qw(shuffle);
use Date::Format qw(time2str);

binmode(STDOUT, ":utf8");

my %handler;
$handler{regproducer} = 'regular';
$handler{org} = 'regular';
$handler{endpoint} = 'regular';
$handler{blacklist} = 'bonus';
$handler{bpjson} = 'bonus';
$handler{history} = 'bonus';
$handler{heartbeat} = 'bonus';
$handler{chains} = 'bonus';
$handler{ipv6} = 'bonus';

# --------------------------------------------------------------------------
# Main

my $data = get_report_options ();
my $chain = chain ();
my $labels = labels ();
my $properties = chain_properties ($chain);
my @classes = classes ();

foreach my $lang (languages()) {
	foreach my $content_type (content_types()) {
		foreach my $entry (@{$$data{producers}}) {
			my @section;

			my $producer = $$entry{regproducer}{owner};
			my $producer_name_html = encode_entities($$entry{info}{name} || $$entry{regproducer}{owner});
			foreach my $message (@{$$entry{messages}}) {
				push (@section, {
					sprintf => "%4s, %11s, %s",
					data => [$$message{kind}, $$message{class}, generate_message($message, lang => $lang, content_type => $content_type)],
					icons => 1,
					class => 2,
					noescape => 3
				});
			}

			my $title = $$labels{title_producers}{"label_$lang"} || $$labels{title_producers}{"label_en"};
			$title =~ s/%producer_name_html%/$producer_name_html/;
			generate_report (chain => $chain, lang => $lang, content_type => $content_type, data => $data, report => [{rows => \@section}], title => $title, columns => 1, outfile => "producers/$producer", text => 1, html => 1);
		}
	}
}

my @entries1;
my @entries2;
my @entries3;

foreach my $entry (shuffle (@{$$data{producers}})) {
	my $votep = $$entry{info}{vote_percent};

	my $check = 0;
	my $msgsummary = $$entry{message_summary};
	foreach my $key (@classes) {
		if (exists $$msgsummary{$key} && $$msgsummary{$key} eq 'ok') {
			$check++;
		}
	}

	if ($check > 0) {
		push (@entries1, $entry);
	} elsif ($votep > 0.5) {
		push (@entries2, $entry);
	} else {
		push (@entries3, $entry);
	}
}

foreach my $lang (languages) {
	my @report;
	push (@report, "<div>\n");
	foreach my $entry (@entries1, @entries2, @entries3) {
		my $producer = $$entry{regproducer}{owner};
		my $rank = $$entry{info}{rank};
		my $votep = $$entry{info}{vote_percent};
		my $producer_name_html = encode_entities($$entry{info}{name} || $$entry{regproducer}{owner});
		my $country = $$entry{info}{country_alpha2};

		my $logo = bp_logo ($entry);
		my %results;

		my %msgsummary = %{$$entry{message_summary}} if (ref $$entry{message_summary});
		foreach my $key (@classes) {
			if ($handler{$key} eq 'regular') {
				$msgsummary{$key} = sev_html(kind => $msgsummary{$key} || 'skip', class => $key, lang => $lang);
			} elsif ($handler{$key} eq 'bonus') {
				if (defined $msgsummary{$key} && ($msgsummary{$key} eq 'ok' || $msgsummary{$key} eq 'info')) {
					$msgsummary{$key} = sev_html(kind => "bonus_$key", class => $key, lang => $lang);
				} else {
					$msgsummary{$key} = sev_html(kind => 'none', class => $key, lang => $lang);
				}
			} else {
				die "$0: unknown hanndler=<$key>";
			}
		}

		my $selected = '';
		if ($rank <= 21) {
			$selected = sev_html(kind => 'bp_top21', class => undef, lang => $lang);
		} elsif ($votep > 0.5) {
			$selected = sev_html(kind => 'bp_standby', class => undef, lang => $lang);
		} else {
			$selected = sev_html(kind => 'bp_other', class => undef, lang => $lang);
		}

		my $flag = '';
		if ($country) {
			$flag = flag_html($country);
		}

		push (@report, "<div id=\"bp_$producer\" class=\"scorecard\" onclick=\"window.location.href='$producer.html'\">\n");
		push (@report, "<div><span class=bp_name>$producer_name_html</span></div>\n");
		push (@report, "<div>\n");
		push (@report, "  <div style=\"display: inline-block; vertical-align: middle; width: 32px\">$logo</div>\n");
		push (@report, "  <div style=\"display: inline-block; vertical-align: middle; width: 28px\">$flag</div>\n");
		push (@report, "  <div style=\"display: inline-block; vertical-align: middle; width: 32px; height: 32px\">$selected</div>\n");
		push (@report, "</div>\n");
		push (@report, "<div>\n");
		push (@report, "  <div style=\"display: inline-block; vertical-align: middle; width: 32px\">$msgsummary{regproducer}</div>\n");
		push (@report, "  <div style=\"display: inline-block; vertical-align: middle; width: 32px\">$msgsummary{org}</div>\n");
		push (@report, "  <div style=\"display: inline-block; vertical-align: middle; width: 32px\">$msgsummary{endpoint}</div>\n");
		push (@report, "</div>\n");
		push (@report, "<div>\n");
		push (@report, "  <div style=\"display: inline-block; vertical-align: middle; width: 32px\">$msgsummary{blacklist}</div>\n") if ($$properties{class_blacklist});
		push (@report, "  <div style=\"display: inline-block; vertical-align: middle; width: 32px\">$msgsummary{bpjson}</div>\n") if ($$properties{class_bpjson});
		push (@report, "  <div style=\"display: inline-block; vertical-align: middle; width: 32px\">$msgsummary{history}</div>\n") if ($$properties{class_history});
		push (@report, "  <div style=\"display: inline-block; vertical-align: middle; width: 32px\">$msgsummary{heartbeat}</div>\n") if ($$properties{class_heartbeat});
		push (@report, "  <div style=\"display: inline-block; vertical-align: middle; width: 32px\">$msgsummary{chains}</div>\n") if ($$properties{class_chains});
		push (@report, "  <div style=\"display: inline-block; vertical-align: middle; width: 32px\">$msgsummary{ipv6}</div>\n") if ($$properties{class_ipv6});
		push (@report, "</div>\n");
		push (@report, "</div>\n");
	}
	push (@report, "</div>\n");

	my @filter;
	foreach my $entry (@entries1, @entries2, @entries3) {
		my $producer = $$entry{regproducer}{owner};
		my $rank = $$entry{info}{rank};
		my $votep = $$entry{info}{vote_percent};

		my %results;
		$results{name} = $producer;

		if ($rank <= 21) {
			push (@{$results{tags}}, 'top21_y');
			push (@{$results{tags}}, 'paid_n');
			push (@{$results{tags}}, 'other_n');
		} elsif ($votep > 0.5) {
			push (@{$results{tags}}, 'top21_n');
			push (@{$results{tags}}, 'paid_y');
			push (@{$results{tags}}, 'other_n');
		} else {
			push (@{$results{tags}}, 'top21_n');
			push (@{$results{tags}}, 'paid_n');
			push (@{$results{tags}}, 'other_y');
		}

		foreach my $key (@classes) {
			if (exists $$entry{message_summary}{$key} && (($$entry{message_summary}{$key} eq 'ok') || ($$entry{message_summary}{$key} eq 'info'))) {
					push (@{$results{tags}}, $key . '_y');
				} else {
					push (@{$results{tags}}, $key . '_n');
				}
		}

		push (@filter, \%results);
	}

	my $final_result = to_json ({
		meta => {generated_at => time2str("%C", time), maintainer => 'matthew@eosnation.io'},
		producers => \@filter
	}, {
		pretty => 0,
		canonical => 1
	});

	my @form;
	push (@form, "<div>\n");
	push (@form, checkbox('top21', sev_html(kind => 'bp_top21', class => undef, lang => $lang, color => 'bw') . ' ' . label('bp_top21', $lang), $lang));
	push (@form, checkbox('paid', sev_html(kind => 'bp_standby', class => undef, lang => $lang, color => 'bw') . ' ' . label('bp_standby', $lang), $lang));
	push (@form, checkbox('other', sev_html(kind => 'bp_other', class => undef, lang => $lang, color => 'bw') . ' ' . label('bp_other', $lang), $lang));
	push (@form, checkbox('regproducer', sev_html(kind => 'check', class => undef, lang => $lang, color => 'bw') . ' ' . label('class_regproducer_l', $lang), $lang));
	push (@form, checkbox('org', sev_html(kind => 'check', class => undef, lang => $lang, color => 'bw') . ' ' . label('class_org_l', $lang), $lang));
	push (@form, checkbox('endpoint', sev_html(kind => 'check', class => undef, lang => $lang, color => 'bw') . ' ' . label('class_endpoint_l', $lang), $lang));
	push (@form, checkbox('blacklist', sev_html(kind => 'bonus_blacklist', class => undef, lang => $lang, color => 'bw') . ' ' . label('class_blacklist_l', $lang), $lang)) if ($$properties{class_blacklist});
	push (@form, checkbox('bpjson', sev_html(kind => 'bonus_bpjson', class => undef, lang => $lang, color => 'bw') . ' ' . label('class_bpjson_l', $lang), $lang)) if ($$properties{class_bpjson});
	push (@form, checkbox('history', sev_html(kind => 'bonus_history', class => undef, lang => $lang, color => 'bw') . ' ' . label('class_history_l', $lang), $lang)) if ($$properties{class_history});
	push (@form, checkbox('heartbeat', sev_html(kind => 'bonus_heartbeat', class => undef, lang => $lang, color => 'bw') . ' ' . label('class_heartbeat_l', $lang), $lang)) if ($$properties{class_heartbeat});
	push (@form, checkbox('chains', sev_html(kind => 'bonus_chains', class => undef, lang => $lang, color => 'bw') . ' ' . label('class_chains_l', $lang), $lang)) if ($$properties{class_chains});
	push (@form, checkbox('ipv6', sev_html(kind => 'bonus_ipv6', class => undef, lang => $lang, color => 'bw') . ' ' . label('class_ipv6_l', $lang), $lang)) if ($$properties{class_ipv6});
	push (@form, "</div>\n");

	unshift (@report, @form);

	push (@report, "<script>\nvar filterData = $final_result\n</script>\n");

	write_report_thtml (chain => $chain, lang => $lang, content => \@report, outfile => "producers/index");
}

sub checkbox {
	my ($name, $label, $lang) = @_;

	my $string = '';
	$string .= "<div class=\"filtergroup\">\n";
	$string .= "$label<br>\n";
	$string .= " <label class=\"radio\"><input class=\"filter\" type=\"radio\" name=\"$name\" value=\"${name}_y\" onClick=\"runFilters()\"> " . label('filter_yes', $lang) . "</label>\n";
	$string .= " <label class=\"radio\"><input class=\"filterx\" type=\"radio\" name=\"$name\" checked onClick=\"runFilters()\"> " . label('filter_na', $lang) ."</label>\n";
	$string .= " <label class=\"radio\"><input class=\"filter\" type=\"radio\" name=\"$name\" value=\"${name}_n\" onClick=\"runFilters()\"> " . label('filter_no', $lang) . "</label>\n";
	$string .= "</div>\n";
	return $string;
}
