#!/usr/bin/perl -w

use utf8;
use strict;
use EOSN::Report;
use EOSN::FileUtil qw(read_file write_file);
use File::Find;
use JSON;
use Getopt::Long;
use Data::Dumper;

binmode(STDOUT, ":utf8");

# ---------------------------------------------------------------------------
# Main

my %globals;
my $labels;
my $data = get_report_options();
my $outdir = outdir();
read_globals();
go();

# ---------------------------------------------------------------------------
# Subroutines

sub read_globals {
	$globals{"template"} = read_file ("$outdir/res/template.html");
	$globals{last_update} = $$data{meta}{generated_at};
	$labels = labels ();
}

sub go {
	find({wanted => \&generate_content, no_chdir => 1}, $outdir);
}

sub generate_content {
	return if ($_ !~ /\.thtml.\w\w$/);
	my $filename_in = $File::Find::name;
	my $filename_out = $filename_in;
	$filename_out =~ s/\.thtml.(\w\w)$/.html.$1/;
	my $lang = $1;
	#print "generate file=<$filename_in> lang=<$lang>\n";
	my (@content) = read_file ($filename_in);

	my $header = 1;
	my %variables;
	foreach my $line (@content) {
		if ($line =~ /^$/ && $header) {
			$header = 0;
		} elsif ($header) {
			my ($key, $value) = split (/\s*=\s*/, $line, 2);
			$variables{$key} = $value;
		} else {
			$variables{content} .= "$line\n";
		}
	}

	my $output = $globals{template};
	$globals{lang} = $lang;

	foreach my $key (keys %$labels) {
		my $value = $$labels{$key}{"label_$lang"} || $$labels{$key}{"label_en"};
		$output =~ s/%$key%/$value/g;
	}

	foreach my $key (keys %globals) {
		my $value = $globals{$key};
		$output =~ s/%$key%/$value/g;
	}	

	foreach my $key (keys %variables) {
		my $value = $variables{$key};
		$output =~ s/%$key%/$value/g;
	}	

	write_file ($filename_out, $output);
}
