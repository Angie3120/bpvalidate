#!/usr/bin/perl -w

use utf8;
use strict;
use EOSN::Report;
use EOSN::FileUtil qw(read_file write_file);
use File::Find;

binmode(STDOUT, ":utf8");

# ---------------------------------------------------------------------------
# Main

my %generated;
my %globals;
my $labels;
get_report_options_website();
my $outdir = outdir();
read_globals();
go();

# ---------------------------------------------------------------------------
# Subroutines

sub read_globals {
	$globals{"template"} = read_file ("$outdir/res/template.html");
	$globals{generated_at} = time2str ("%C", time);

	$labels = labels ();

        foreach my $chain (chains()) {
		my $properties = chain_properties ($chain);
		next if (! $$properties{active});
		$generated{navigation_producers} .= (' ' x 8) . '<a class="navbar-item" href="/' . $chain . '/producers/">%chain_' . $chain . '%</a>' . "\n";
		$generated{navigation_reports} .= (' ' x 8) . '<a class="navbar-item" href="/' . $chain . '/reports/">%chain_' . $chain . '%</a>' . "\n";
		$generated{navigation_data} .= (' ' x 8) . '<a class="navbar-item" href="/' . $chain . '/data/">%chain_' . $chain . '%</a>' . "\n";
		$generated{networks} .= (' ' x 4) . '<li> <a href="' . $chain . '/">%chain_' . $chain . '%</a>' . "\n";
	}
}

sub go {
	find({wanted => \&generate_content, no_chdir => 1}, $outdir);
}

sub generate_content {
	return if ($_ !~ /\.thtml.\w\w$/);
	my $filename_in = $File::Find::name;
	my $filename_out = $filename_in;
	$filename_out =~ s/\.thtml.(\w\w)$/.html.$1/;
	return if ($filename_in =~ m#${outdir}res/#);
	my $lang = $1;
	#print "generate file=<$filename_in> lang=<$lang>\n";
	my (@content) = read_file ($filename_in);

	my $header = 1;
	my %variables;
	foreach my $line (@content) {
		if ($line =~ /^$/ && $header) {
			$header = 0;
		} elsif ($header) {
			my ($key, $value) = split (/\s*=\s*/, $line, 2);
			$variables{$key} = $value;
		} else {
			$variables{content} .= "$line\n";
		}
	}

	my $output = $globals{template};
	$globals{lang} = $lang;
	$globals{last_update} = datetime($globals{generated_at}, $lang);
	if ($variables{chain}) {
		$globals{chain} = $$labels{'chain_' . $variables{chain}}{"label_$lang"} || $variables{chain};
		$globals{chain_title} = ($$labels{'chain_' . $variables{chain}}{"label_$lang"} || $variables{chain}) . ": ";
	} else {
		$globals{chain} = '--';
		$globals{chain_title} = '';
	}

	foreach my $key (keys %variables) {
		my $value = $variables{$key};
		$output =~ s/%$key%/$value/g;
	}

	foreach my $key (keys %generated) {
		my $value = $generated{$key};
		$output =~ s/%$key%/$value/g;
	}

	foreach my $key (keys %$labels) {
		my $value = $$labels{$key}{"label_$lang"} || $$labels{$key}{"label_en"};
		$output =~ s/%$key%/$value/g;
	}

	foreach my $key (keys %globals) {
		my $value = $globals{$key};
		$output =~ s/%$key%/$value/g;
	}	

	write_file ($filename_out, $output);
}
