#!/usr/bin/perl -w

use utf8;
use strict;
use EOSN::FileUtil qw(read_file write_file);
use File::Find;
use JSON;
use Getopt::Long;

binmode(STDOUT, ":utf8");

# ---------------------------------------------------------------------------
# Main

my $infile = undef;
my $outdir = "/var/www/html";
my %globals;

GetOptions('input=s' => \$infile, 'output=s' => \$outdir) || exit 1;

die "$0: filename not given" if (! $infile);
die "$0: filename not given" if (! $infile);

read_globals();
go();

# ---------------------------------------------------------------------------
# Subroutines

sub read_globals {
	$globals{template_en} = read_file ("$outdir/res/template.html.en");
	$globals{template_fr} = read_file ("$outdir/res/template.html.fr");

	my $data = from_json(read_file($infile) || die "$0: no data read");
	$globals{last_update} = $$data{meta}{generated_at};
}

sub go {
	find({wanted => \&generate_content, no_chdir => 1}, $outdir);
}

sub generate_content {
	return if ($_ !~ /\.thtml.\w\w$/);
	my $filename_in = $File::Find::name;
	my $filename_out = $filename_in;
	$filename_out =~ s/\.thtml.(\w\w)$/.html.$1/;
	my $lang = $1;
	print "generate file=<$filename_in> lang=<$lang>\n";
	my (@content) = read_file ($filename_in);

	my $header = 1;
	my %variables;
	foreach my $line (@content) {
		if ($line =~ /^$/ && $header) {
			$header = 0;
		} elsif ($header) {
			my ($key, $value) = split (/\s*=\s*/, $line, 2);
			$variables{$key} = $value;
		} else {
			$variables{content} .= "$line\n";
		}
	}

	my $output = $globals{"template_$lang"};

	foreach my $key (keys %globals) {
		my $value = $globals{$key};
		$output =~ s/%$key%/$value/g;
	}	
	foreach my $key (keys %variables) {
		my $value = $variables{$key};
		$output =~ s/%$key%/$value/g;
	}	

	write_file ($filename_out, $output);
}
